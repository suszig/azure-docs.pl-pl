---
title: "Moduły Terraform używany do tworzenia obciążenia zrównoważonym maszyny Wirtualnej systemu Linux infrastruktury na platformie Azure"
description: "Dowiedz się, jak utworzyć klaster maszyny wirtualnej systemu Linux z modułem równoważenia obciążenia na platformie Azure przy użyciu modułów Terraform"
keywords: "terraform, devops, maszyny wirtualnej, sieci, modułów"
author: rloutlaw
manager: justhe
ms.service: virtual-machines-linux
ms.custom: devops
ms.topic: article
ms.date: 10/19/2017
ms.author: routlaw
ms.openlocfilehash: 86fee5446065ffa8b5e8186ba8d8f56aa3efc2ea
ms.sourcegitcommit: b979d446ccbe0224109f71b3948d6235eb04a967
ms.translationtype: MT
ms.contentlocale: pl-PL
ms.lasthandoff: 10/25/2017
---
# <a name="create-a-vm-cluster-with-terraform-using-custom-hcl"></a>Tworzenie klastra maszyny Wirtualnej z Terraform przy użyciu niestandardowych HCL

W tym samouczku pokazano tworzenie klastra obliczeniowego małych za pomocą [Hashicorp konfiguracji języka](https://www.terraform.io/docs/configuration/syntax.html) (HCL). Konfiguracja tworzy moduł równoważenia obciążenia, dwóch maszyn wirtualnych systemu Linux w [zestawu dostępności](/azure/virtual-machines/windows/manage-availability#configure-multiple-virtual-machines-in-an-availability-set-for-redundancy)i wszystkie niezbędne zasoby sieciowe.

W tym samouczku obejmują:

> [!div class="checklist"]
> * Konfigurowanie uwierzytelniania w systemie Azure
> * Utwórz szablon Terraform
> * Wizualizuj zmiany z planem
> * Zastosuj konfigurację, aby utworzyć klaster

## <a name="set-up-authentication-with-azure"></a>Konfigurowanie uwierzytelniania w systemie Azure

> [!TIP]
> Jeśli użytkownik [używać zmiennych środowiskowych Terraform](/azure/virtual-machines/linux/terraform-install-configure#set-environment-variables) lub uruchomione w tym samouczku [powłoki chmury Azure](/azure/cloud-shell/overview), Pomiń ten krok.

 Przegląd [Terraform zainstalować i skonfigurować dostęp do usługi Azure](/azure/virtual-machines/linux/terraform-install-configure) można utworzyć nazwy głównej usługi platformy Azure. Użyj tej nazwy głównej usługi w celu wypełnienia nowego pliku `azureProviderAndCreds.tf` w pustego katalogu z następującym kodem:

```tf
variable subscription_id {}
variable tenant_id {}
variable client_id {}
variable client_secret {}

provider "azurerm" {
    subscription_id = "xxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx"
    tenant_id = "xxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx"
    client_id = "xxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx"
    client_secret = "xxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx"
}
```

## <a name="create-the-template"></a>Tworzenie szablonu

Utwórz nowy szablon Terraform o nazwie `main.tf` z następującym kodem: 

```tf
resource "azurerm_resource_group" "test" {
  name     = "acctestrg"
  location = "West US 2"
}

resource "azurerm_virtual_network" "test" {
  name                = "acctvn"
  address_space       = ["10.0.0.0/16"]
  location            = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"
}

resource "azurerm_subnet" "test" {
  name                 = "acctsub"
  resource_group_name  = "${azurerm_resource_group.test.name}"
  virtual_network_name = "${azurerm_virtual_network.test.name}"
  address_prefix       = "10.0.2.0/24"
}

resource "azurerm_public_ip" "test" {
  name                         = "publicIPForLB"
  location                     = "${azurerm_resource_group.test.location}"
  resource_group_name          = "${azurerm_resource_group.test.name}"
  public_ip_address_allocation = "static"
}

resource "azurerm_lb" "test" {
  name                = "loadBalancer"
  location            = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"

  frontend_ip_configuration {
    name                 = "publicIPAddress"
    public_ip_address_id = "${azurerm_public_ip.test.id}"
  }
}

resource "azurerm_lb_backend_address_pool" "test" {
  resource_group_name = "${azurerm_resource_group.test.name}"
  loadbalancer_id     = "${azurerm_lb.test.id}"
  name                = "BackEndAddressPool"
}

resource "azurerm_network_interface" "test" {
  count               = 2
  name                = "acctni${count.index}"
  location            = "${azurerm_resource_group.test.location}"
  resource_group_name = "${azurerm_resource_group.test.name}"

  ip_configuration {
    name                          = "testConfiguration"
    subnet_id                     = "${azurerm_subnet.test.id}"
    private_ip_address_allocation = "dynamic"
    load_balancer_backend_address_pools_ids = ["${azurerm_lb_backend_address_pool.test.id}"]
  }
}

resource "azurerm_managed_disk" "test" {
  count                = 2
  name                 = "datadisk_existing_${count.index}"
  location             = "${azurerm_resource_group.test.location}"
  resource_group_name  = "${azurerm_resource_group.test.name}"
  storage_account_type = "Standard_LRS"
  create_option        = "Empty"
  disk_size_gb         = "1023"
}

resource "azurerm_availability_set" "avset" {
  name                         = "avset"
  location                     = "${azurerm_resource_group.test.location}"
  resource_group_name          = "${azurerm_resource_group.test.name}"
  platform_fault_domain_count  = 2
  platform_update_domain_count = 2
  managed                      = true
}

resource "azurerm_virtual_machine" "test" {
  count                 = 2
  name                  = "acctvm${count.index}"
  location              = "${azurerm_resource_group.test.location}"
  availability_set_id   = "${azurerm_availability_set.avset.id}"
  resource_group_name   = "${azurerm_resource_group.test.name}"
  network_interface_ids = ["${element(azurerm_network_interface.test.*.id, count.index)}"]
  vm_size               = "Standard_DS1_v2"

  # Uncomment this line to delete the OS disk automatically when deleting the VM
  # delete_os_disk_on_termination = true

  # Uncomment this line to delete the data disks automatically when deleting the VM
  # delete_data_disks_on_termination = true

  storage_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "16.04-LTS"
    version   = "latest"
  }

  storage_os_disk {
    name              = "myosdisk${count.index}"
    caching           = "ReadWrite"
    create_option     = "FromImage"
    managed_disk_type = "Standard_LRS"
  }

  # Optional data disks
  storage_data_disk {
    name              = "datadisk_new_${count.index}"
    managed_disk_type = "Standard_LRS"
    create_option     = "Empty"
    lun               = 0
    disk_size_gb      = "1023"
  }

  storage_data_disk {
    name            = "${element(azurerm_managed_disk.test.*.name, count.index)}"
    managed_disk_id = "${element(azurerm_managed_disk.test.*.id, count.index)}"
    create_option   = "Attach"
    lun             = 1
    disk_size_gb    = "${element(azurerm_managed_disk.test.*.disk_size_gb, count.index)}"
  }

  os_profile {
    computer_name  = "hostname"
    admin_username = "testadmin"
    admin_password = "Password1234!"
  }

  os_profile_linux_config {
    disable_password_authentication = false
  }

  tags {
    environment = "staging"
  }
}
```

## <a name="visualize-the-changes-with-plan"></a>Wizualizuj zmiany z planem

Uruchom `terraform plan` do podglądu infrastruktury maszyny wirtualnej utworzonej przez szablon.

![Terraform Plan](media/terraformPlanVmsWithModules.png)


## <a name="create-the-virtual-machines-with-apply"></a>Tworzenie maszyn wirtualnych z Zastosuj

Uruchom `terraform apply` do udostępniania klastra maszyny Wirtualnej na platformie Azure.

![Zastosuj Terraform](media/terraformApplyVmsWithModules.png)

## <a name="next-steps"></a>Następne kroki

- Przejrzyj listę [modułów Azure Terraform](https://registry.terraform.io/modules/Azure)
- Utwórz [zestawu skalowania maszyn wirtualnych z Terraform]()